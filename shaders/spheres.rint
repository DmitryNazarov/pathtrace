#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_ray_query : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : enable
#include "raycommon.glsl"

layout(binding = 5, set = 0) buffer Spheres { Sphere s[]; } spheres;

// Ray-Sphere intersection
// http://viclw17.github.io/2018/07/16/raytracing-ray-sphere-intersection/
float hitSphere(const Sphere s, const Ray r)
{
	vec3 oc = r.origin - s.pos;
	float a = dot(r.direction, r.direction);
	float b = 2.0 * dot(oc, r.direction);
	float c = dot(oc, oc) - s.radius * s.radius;
	float discriminant = b * b - 4 * a * c;
	if (discriminant < 0.0)
	{
		return -1.0;
	}

	float numerator = -b - sqrt(discriminant);
	if (numerator > 0.0)
	{
		return numerator / (2.0 * a);
	}
	numerator = -b + sqrt(discriminant);
	if (numerator > 0.0)
	{
		return numerator / (2.0 * a);
	}
	else
	{
		return -1.0;
	}
}

void main()
{
	Sphere s = spheres.s[gl_PrimitiveID];
	Ray rayTransf;
	rayTransf.origin = (s.invertedTransform * vec4(gl_WorldRayOriginEXT, 1.0f)).xyz;
	rayTransf.direction = (normalize(s.invertedTransform * vec4(gl_WorldRayDirectionEXT, 0.0f))).xyz;

	float tHit = hitSphere(s, rayTransf);
	if (tHit > 0.0)
	{
		vec3 intersectionPointTransf = rayTransf.origin + tHit * rayTransf.direction;
		vec3 intersectionPoint = (s.transform * vec4(intersectionPointTransf, 1.0f)).xyz;
		tHit = length(intersectionPoint - gl_WorldRayOriginEXT);
		reportIntersectionEXT(tHit, 0);
	}
}